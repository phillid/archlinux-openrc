# Maintainer : David Phillips <david sighup d.t nz>
# Contributor:  Andrew Gregory <andrew.gregory.8@gmail.com>
# Contributor: Lone_Wolf <lonewolf at xs4all dot nl>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=openrc
pkgver=0.31.1
pkgrel=1
pkgdesc='Dependency-based init system that works with sysvinit.'
arch=('i686' 'x86_64')
url='https://github.com/OpenRC/openrc/'
license=('BSD')
depends=('openrc-sysvinit' 'pam' 'sh')
optdepends=('openrc-arch-services-git: collection of services for Arch'
            'net-tools: for network service support'
            'opentmpfiles: adds support for systemd-style tmpfiles.d')
backup=(etc/openrc/inittab
        etc/openrc/rc.conf
        etc/openrc/conf.d/{bootmisc,consolefont,devfs,dmesg,fsck,hostname,hwclock,keymaps}
        etc/openrc/conf.d/{killprocs,localmount,modules,netmount,network,staticroute}
        )
source=("$pkgname-$pkgver::https://github.com/OpenRC/$pkgname/archive/$pkgver.tar.gz"
        "$pkgname.logrotate")
sha512sums=('98001e05896d113e20fad27ba100e2db5588262cb9c0b52c2800b38fd320b9baf4a99302044ef4045ba3a746d685d0cc27f8fdb42dccbc0574e32c6db9e6d3d2'
            '690612fddfb2c4cf8f6b5ba7239b9faf29eb3d9b152ab4dcf62694aa2852780440d08cee56d98a9597607f446b3697c911269562821a8402bb5747cbbae34bd9')


# LIBMODE to enable binary stripping by makepkg
MAKEFLAGS="BRANDING='Arch Linux'\
           MKPAM=pam \
           MKSELINUX=no \
           MKTERMCAP=ncurses \
           PKG_PREFIX="" \
           LIBDIR=/usr/lib \
           LIBMODE=0644 \
           SHLIBDIR=/usr/lib \
           LIBEXECDIR=/usr/lib/openrc \
           BINDIR=/usr/bin \
           SBINDIR=/usr/bin \
           SYSCONFDIR=/etc/openrc"

build() {
    cd "${pkgname}-${pkgver}"
    make
}

package() {

    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # avoid initscripts conflict, requires openrc-sysvinit
    install -m 644 support/sysvinit/inittab "${pkgdir}/etc/openrc/inittab"

    # rotate boot log
    install -Dm0644 "${srcdir}/${pkgname}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgname}"

    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${srcdir}/${pkgname}-${pkgver}/AUTHORS" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
